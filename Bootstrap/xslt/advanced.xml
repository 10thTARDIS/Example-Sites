<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0">
    <xsl:output indent="yes" omit-xml-declaration="yes"/>
    <xsl:template match="/system-index-block/calling-page/system-page">
        <div class="row">
            <div class="col-xs-12">
                <h1>
                    <xsl:value-of select="title"/>
                </h1>
            </div>
        </div>
        <xsl:apply-templates select="system-data-structure/row"/>
    </xsl:template>
    <xsl:template match="row">
        <hr/>
        <div class="row">
            <xsl:variable name="numColumns" select="count(column)"/>
            <xsl:apply-templates select="column">
                <xsl:with-param name="width" select="12 div $numColumns"/>
            </xsl:apply-templates>
        </div>
    </xsl:template>
    <xsl:template match="column">
        <xsl:param name="width"/>
        <div class="col-xs-12 col-md-{$width}">
            <xsl:if test="features[value='Heading']/value">
                <h2>
                    <xsl:value-of select="heading"/>
                </h2>
            </xsl:if>
            <xsl:if test="features[value='Image+Link']/value">
                <xsl:choose>
                    <xsl:when test="type = 'Internal'">
                        <a href="{internal/link}">
                            <xsl:call-template name="makeImage"/>
                        </a>
                    </xsl:when>
                    <xsl:when test="type = 'Custom'">
                        <a href="{custom}">
                            <xsl:call-template name="makeImage"/>
                        </a>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:call-template name="makeImage"/>
                    </xsl:otherwise>
                </xsl:choose>
            </xsl:if>
            <xsl:if test="features[value='WYSIWYG']/value">
                <xsl:copy-of select="content/node()"/>
            </xsl:if>
            <xsl:if test="features[value='Block']/value">
                <xsl:copy-of select="block/content/node()"/>
            </xsl:if>
        </div>
    </xsl:template>
    <xsl:template name="makeImage">
        <img alt="{alt}" class="img-responsive" src="{image/link}"/>
    </xsl:template>
</xsl:stylesheet>